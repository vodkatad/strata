include: "conf.sk"
#1 for other muts file
rule reduce_mut_gene:
    input: MUTS
    output: "muts_gene.txt"
    shell:
        """
            sed 1d {input} | perl -F"\\t" -ane 'printf("CRC%04d\\t%s\\n",$F[0],$F[3])' > {output}
        """

rule reduce_cnv_gene:
    input: CNV
    output: "cnv.txt"
    shell:
        """
            sed 1d {input} | perl -F"\\t" -ane 'printf("CRC%04d\\t%s\\t%s\\n",$F[0],$F[1],$F[5])' > {output}
        """

rule muts_gene_table:
    input: "muts_{kind}.txt"
    output: "muts_{kind}_pivot.txt"
    run:
         import pandas
         data = pandas.read_csv(input[0], sep="\t", header=None, names=['pdx','mut'])
         pt = pandas.pivot_table(data, index=['pdx'], columns='mut', aggfunc=lambda x: 1, fill_value=0)
         pt.to_csv(output[0], sep='\t', index=True)

rule sanger_all:
    input: mut=SANGER, msi=MSI_TRUE
    output: "muts_sanger.txt"
    shell:
        """
            grep LMX {input.mut} |  perl -ane 'my $c=substr($F[0],0,7); print "$c\\t$F[7]\\n"' | filter_1col -v 1 {input.msi} > {output}
        """

wildcard_constraints:
    thr="[0-9]"

rule cnv:
    input: mut="cnv.txt", msi=MSI_TRUE
    output: "muts_cnv{thr}.txt"
    shell:
        """
        sed 1d {input.mut} | bawk '$3 != "NA" && $3 >= {wildcards.thr} {{print $1,$2"_gain"}} $3 == "NA" {{print $1,$2"_loss"}}' | filter_1col -v 1 {input.msi} > {output}
        """

rule avg_week:
    input: volumes=VOLUMES
    output: waterfall="{wr}-{wt}_waterfall.png", means="{wr}-{wt}_avg.tsv"
    params: debug=DEBUG, pdx="pdx", ref="{wr}", target="{wt}"  
    script: SRC_DIR+"average_volumes_pdx.R"

rule models:
    input: volumes=W3, pivot="muts_{type}_pivot.txt", msi=MSI, gsannot=GS_DESC
    output: linear="{type}_linear_models.tsv", logistic="{type}_logistic_models.tsv", linearxls="{type}_linear_models.xls"
    params: debug=DEBUG, pdx="pdx", cutoff=3, plotdir="{type}_plots"
    script: SRC_DIR+"models_vol_pivot.R"

# TODO R that add gene names to linear models, sorts on pvalue and add extended gene name - plots of volumes 0 vs 1

#### Pathways
# do not work with CNV right now, _gain _loss is there.
rule translate_gene_sign:
    input: muts="muts_{kind}.txt", path=PATHWAYS
    output: "muts_{kind}pathways.txt"
    shell:  
        """
            translate -n -a -d -j -k <(bawk '{{print $1"_"$2,$3}}' < {input.path} | sort | uniq) 2 < {input.muts} | cut -f 1,3 | sort | uniq > {output}
        """

#### Annotation
#[exomes_other_dec18]egrassi@hactarlogin$ head -n 2 ../../local/share/data/pre_irino/dec2018/../DTB_WESdata_CompleteStudy_muts.txt | cut -f 7,27,28,29
# -1 to fall to 0 based


#TODO SANGER
rule detailed_muts:
    input: "../../local/share/data/pre_irino/DTB_WESdata_CompleteStudy_muts_manualfixes.txt"
    output: "muts_gene.annovar.bed"
    shell:
        """
            sed 1d {input} | perl -F"\\t" -ane 'my $g = $F[3]; $g =~ tr/\-_:/;;;;/; printf("CRC%04d\\t%s\\t%s\\t%s\\t%s\\t%s\\n",$F[0],$g,$F[6],$F[26],$F[27],$F[28])' \\
            | tr ":" "_" | tr "_" "\\t" | tr "-" "\\t" | bawk '{{print $3,$4-1,$5,$6"-"$7"-"$1"_"$2"_"$8"_"$9"_"$10}}' > {output}
        """

rule liftover:
    input: "muts_gene.annovar.bed", "../../local/share//data/pre_irino/hg18ToHg38.over.chain.gz"
    output: "muts_gene.annovar.hg38.bed", "muts_gene.annovar.hg38.bed.miss"
    shell:
        """
            liftOver {input[0]} {input[1]} {output[0]} {output[1]}
        """

rule annovar:
    input: "muts_gene.annovar.hg38.bed", "/home/egrassi/common/task/annotations/dataset/annovar/hg38/humandb/"
    output: "wes.hg38_multianno.txt"
    params: ver="hg38"
    shell: 
        """
        sed 's/chr//1;' < {input[0]} | tr "-" "\\t" | bawk '{{b=$2+1; e=b+length($4)-1; print $1,b,e,$4,$5,$6}}' > {output}.tmp
        table_annovar.pl {output}.tmp {input[1]} --otherinfo -buildver {params.ver} -out wes -remove -protocol refGene,avsnp150,cosmic87_coding,nci60 -operation g,f,f,f -nastring . -polish 
        rm {output}.tmp
        """
