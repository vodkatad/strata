include: "conf.sk"
#3 for other muts file
rule reduce_mut_gene:
    input: MUTS
    output: "muts_gene.txt"
    shell:
        """
            sed 1d {input} | perl -F"\\t" -ane 'printf("CRC%04d\\t%s\\n",$F[0],$F[1])' > {output}
        """

rule reduce_cnv_gene:
    input: CNV
    output: "cnv.txt"
    shell:
        """
            sed 1d {input} | perl -F"\\t" -ane 'printf("CRC%04d\\t%s\\t%s\\n",$F[0],$F[1],$F[5])' > {output}
        """

rule muts_gene_table:
    input: "muts_{kind}.txt"
    output: "muts_{kind}_pivot.txt"
    run:
         import pandas
         data = pandas.read_csv(input[0], sep="\t", header=None, names=['pdx','mut'])
         pt = pandas.pivot_table(data, index=['pdx'], columns='mut', aggfunc=lambda x: 1, fill_value=0)
         pt.to_csv(output[0], sep='\t', index=True)

rule sanger_all:
    input: SANGER
    output: "muts_sanger.txt"
    shell:
        """
            grep LMX {input} |  perl -ane 'my $c=substr($F[0],0,7); print "$c\\t$F[7]\\n"' > {output}
        """

rule sanger_driver:
    input: SANGER
    output: "muts_sangerdriver.txt"
    shell:
        """
            grep LMX {input} |  perl -ane 'if ($F[17] =~ /TRUE/i) {{my $c=substr($F[0],0,7); print "$c\\t$F[7]\\n"}}' > {output}
        """

wildcard_constraints:
    thr="[0-9]"

rule cnv:
    input: "cnv.txt"
    output: "muts_cnv{thr}.txt"
    shell:
        """
        sed 1d {input} | bawk '$3 != "NA" && $3 >= {wildcards.thr} {{print $1,$2"_gain"}} $3 == "NA" {{print $1,$2"_loss"}}' > {output}
        """

rule avg_week:
    input: volumes=VOLUMES
    output: waterfall="{wr}-{wt}_waterfall.png", means="{wr}-{wt}_avg.tsv"
    params: debug=DEBUG, pdx="pdx", ref="{wr}", target="{wt}"  
    script: SRC_DIR+"average_volumes_pdx.R"

rule models:
    input: volumes=W3, pivot="muts_{type}_pivot.txt", msi=MSI, gsannot=GS_DESC
    output: linear="{type}_linear_models.tsv", logistic="{type}_logistic_models.tsv", linearxls="{type}_linear_models.xls"
    params: debug=DEBUG, pdx="pdx", cutoff=3, plotdir="{type}_plots"
    script: SRC_DIR+"models_vol_pivot.R"

# TODO R that add gene names to linear models, sorts on pvalue and add extended gene name - plots of volumes 0 vs 1

#### Pathways
# do not work with CNV right now, _gain _loss is there.
rule translate_gene_sign:
    input: muts="muts_{kind}.txt", path=PATHWAYS
    output: "muts_{kind}pathways.txt"
    shell:  
        """
            translate -n -a -d -j -k <(bawk '{{print $1"_"$2,$3}}' < {input.path} | sort | uniq) 2 < {input.muts} | cut -f 1,3 | sort | uniq > {output}
        """

